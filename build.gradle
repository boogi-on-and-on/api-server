buildscript {
    ext {
        queryDslVersion = "5.0.0"
    }
}

plugins {
    id 'org.springframework.boot' version '2.6.3'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id "com.ewerk.gradle.plugins.querydsl" version "1.0.10"
    id 'java'
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
}


group = 'boogi'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    querydsl.extendsFrom compileClasspath
    asciidoctorExt
}

repositories {
    mavenCentral()
}

ext {
    snippetsDir = file('build/generated-snippets')
    asciidocVersion = "2.0.7.RELEASE"
}

dependencies {
    //Spring MVC, JPA
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    //Spring redis, Spring session
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.session:spring-session-data-redis'

    //Spring rest docs
    asciidoctorExt "org.springframework.restdocs:spring-restdocs-asciidoctor:${asciidocVersion}"
    testImplementation "org.springframework.restdocs:spring-restdocs-mockmvc:${asciidocVersion}"

    //Logging
    implementation 'org.zalando:logbook-spring-boot-starter:2.14.0'
    implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.8.0'

    //actuator, prometheus
    implementation 'io.micrometer:micrometer-registry-prometheus'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    //netty
    implementation 'io.netty:netty-all:4.1.77.Final'
    testImplementation 'io.projectreactor:reactor-test'
    implementation 'io.netty:netty-resolver-dns-native-macos:4.1.77.Final:osx-aarch_64'

    //QueryDSL
    implementation "com.querydsl:querydsl-jpa:${queryDslVersion}"
    implementation "com.querydsl:querydsl-apt:${queryDslVersion}"

    //DB
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'mysql:mysql-connector-java'

    //Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    //rest assured
    testImplementation 'io.rest-assured:rest-assured'

    //google guava
    implementation 'com.google.guava:guava:31.1-jre'

    //Embedded redis
    testImplementation 'it.ozimov:embedded-redis:0.7.2'

    //Test container
    testImplementation "org.testcontainers:junit-jupiter:1.18.1"
    testImplementation "org.testcontainers:mysql:1.18.1"
}

tasks.named('test') {
    useJUnitPlatform()
    outputs.dir snippetsDir
}

asciidoctor {
    inputs.dir snippetsDir
    configurations 'asciidoctorExt'
    dependsOn test
}

asciidoctor.doFirst {
    delete file("src/main/resources/static/docs/*")
}

bootJar {
    dependsOn asciidoctor
    copy {
        from "${asciidoctor.outputDir}"
        into 'src/main/resources/static/docs'
    }
}

def querydslDir = "$buildDir/generated/querydsl"

querydsl {
    jpa = true
    querydslSourcesDir = querydslDir
}
compileQuerydsl {
    options.annotationProcessorPath = configurations.querydsl
}


def profile = project.hasProperty("profile") ? project.property("profile").toString() : "local"

sourceSets {
    main {
        java {
            srcDirs = [querydslDir, "src/main/java"]
        }
        resources {
            srcDirs = ["src/main/resources", "src/main/resources-${profile}"]
        }
    }
}
